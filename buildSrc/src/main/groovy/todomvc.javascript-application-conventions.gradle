plugins {
  id 'base'
  id 'com.github.node-gradle.node'
}

node {
  download = true
  version = "12.20.1"
}

task npmBuild(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'build']
  inputs.dir(project.fileTree('src').exclude('**/*.test.ts*'))
  inputs.dir('public')
  inputs.dir('node_modules')
  inputs.file('tsconfig.json')
  outputs.dir('build')
}
assemble.dependsOn(npmBuild)

task npmStart(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['start']
  outputs.upToDateWhen { true }
}

task npmStorybook(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'storybook']
  outputs.upToDateWhen { true }
}

task npmTest(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'test']
  args = ['--', '--watchAll=false']
  inputs.dir('src')
  inputs.dir('node_modules')
  inputs.file('tsconfig.json')
  outputs.upToDateWhen { true }
}
check.dependsOn(npmTest)

task npmPrettierCheck(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'prettier:check']
  inputs.dir('src')
  inputs.file('.prettierrc.json')
  outputs.upToDateWhen { true }
}
check.dependsOn(npmPrettierCheck)

task npmPrettierWrite(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'prettier:write']
  inputs.dir('src')
  inputs.file('.prettierrc.json')
  outputs.upToDateWhen { true }
}
